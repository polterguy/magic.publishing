
/*
 * Initialises your database as follows.
 *
 * 1. Creates your database by running the "/init/scheme.sql" file
 * 2. Inserts all default templates from the "/init/templates/" folder.
 *
 * Notice, if the database alread exists, it will not do anything.
 */
mysql.connect

   /*
    * Wrapping the rest of the file in a try/catch, which will make
    * sure we don't prematurely kill process, in case database already
    * exists from before.
    */
   try

      /*
       * Loads up your schema file, and executes it.
       */
      log.info:Trying to create magic_publishing database
      io.file.load:/init/schema.sql
      mysql.execute:x:@io.file.load
      log.info:magic_publishing database successfully created

      /*
       * Lists all default template files, loads them, and inserts them into
       * template table.
       */
      log.info:Trying to insert all default templates from /init/templates/ folder
      io.file.list:/init/templates/
      for-each:x:@io.file.list/*

         strings.split:x:@.dp/#
            .:/
         strings.split:x:@strings.split/0/-
            .:.
         io.file.load:x:@.dp/#
         mysql.execute:insert into templates (name, content) values (@name, @content)
            @name:x:@strings.split/0
            @content:x:@io.file.load

      log.info:All default templates successfully inserted into magic_publishing database

      /*
       * Creating a dummy default landing page.
       */
      mysql.execute:@"insert into items (url, author, template, item_type, title, content)
        values ('', 'admin', @template, 'page', @title, @content)"
         @template:default
         @title:Home
         @content:Your default Magic Publishing page. Edit it in your dashboard.

      /*
       * Making sure the default CSS file to use is "simple.css"
       */
      mysql.execute:@"insert into settings (name, value) values ('css-file', 'css/simple.css')"

   .catch

      /*
       * This is probably due to that database exists from before, and hence
       * of no real importance - Still we log it to be on the safe side.
       */
      log.info:"Couldn't initialise database for some reasons. See the next log entry for details."
      log.error:x:@.arguments/*/message
